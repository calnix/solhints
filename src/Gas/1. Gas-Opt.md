# Gas Opt (Rareskills)

## Avoid zero to one storage writes where possible

- Initializing a storage variable is one of the most expensive operations a contract can do.
- When a storage variable goes from zero to non-zero, the user must pay 22,100 gas total (20,000 gas for a zero to non-zero write and 2,100 for a cold storage access).
- This is why the Openzeppelin reentrancy guard registers functions as active or not with 1 and 2 rather than 0 and 1.
- **It only costs 5,000 gas to alter a storage variable from non-zero to non-zero.**

## Cache storage variables: write and read storage variables exactly once

```java
contract Counter1 {
    uint256 public number;

    function moreGas() public {
        require(number < 10);
        number = number + 1;
    }

    function lessGas() public {
        uint256 _number = number;

        require(_number < 10);
        number = _number + 1;
    }
}
```

Reading from a warm storage variable slot at least 100 gas, as Solidity does not cache the storage read.

## Pack related variables

Packing related variables into same slot reduces gas costs by minimizing costly storage related operations.

### Manual packing is the most efficient

- We store and retrieve two uint80 values in one variable (uint160) by using bit shifting.
- This will use only one storage slot and is cheaper when storing or reading the individual values in a single transaction.

```java
contract GasSavingExample {
    uint160 public packedVariables;

    function packVariables(uint80 x, uint80 y) external {
        packedVariables = uint160(x) << 80 | uint160(y);
    }

    function unpackVariables() external view returns (uint80, uint80) {
        uint80 x = uint80(packedVariables >> 80);
        uint80 y = uint80(packedVariables);
        return (x, y);
    }
}
```

### EVM Packing is slightly less efficient

- This also uses one slot like the above example, but may be slightly expensive when storing or reading values in a single transaction.
- This is because the EVM will do the bit-shifting itself.

```java
contract GasSavingExample2 {
    uint80 public var1;
    uint80 public var2;

    function updateVars(uint80 x, uint80 y) external {
        var1 = x;
        var2 = y;
    }

    function loadVars() external view returns (uint80, uint80) {
        return (var1, var2);
    }
}
```

## Variables that are never updated should be immutable or constant

- Constants and immutable values are embedded directly into the bytecode of the contract which they are defined and does not use storage because of this.
- This saves a lot of gas as we do not make any storage reads which are costly.

## Using mappings instead of arrays to avoid length checks

- When storing a list or group of items that you wish to organize in a specific order and fetch with a fixed key/index, it’s common practice to use an array data structure.
- But by using a mapping, we can save 2102 gas.

```java
/// get(0) gas cost: 4860 
contract Array {
    uint256[] a;

    constructor() {
        a.push() = 1;
        a.push() = 2;
        a.push() = 3;
    }

    function get(uint256 index) external view returns(uint256) {
        return a[index];
    }
}

/// get(0) gas cost: 2758
contract Mapping {
    mapping(uint256 => uint256) a;

    constructor() {
        a[0] = 1;
        a[1] = 2;
        a[2] = 3;
    }

    function get(uint256 index) external view returns(uint256) {
        return a[index];
    }
}
```

### Why?

- Under the hood when you read the value of an index of an array, solidity adds bytecode that checks that you are reading from a valid index (i.e an index strictly less than the length of the array), else it reverts with a panic error (Panic(0x32) to be precise).
- This prevents from reading unallocated or worse, allocated storage/memory locations.

Mappings do not have such a check, and therefore save gas.
> It’s important to note that when using mappings in this manner, your code should ensure that you are not reading an out of bound index of your canonical array.

Alternatively, use unsafeAccess function OZ's [Array.sol](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/utils/Arrays.sol). 
Allows developers to directly access values of any given index of an array while skipping the length overflow check.
Still important to ensure you not passing an invalid index.

## Use bitmaps instead of bools when a significant amount of booleans are used

Note that a bool is a uint8 under the hood, this means it's using up 8 bits while it only needs 1 bit: 0/1.
Since it only takes one bit to store this information, and each slot is 256 bits, that means one can store a 256 flags/booleans with one storage slot.

You can learn more about this technique from these resources:

[Video tutorial](https://www.youtube.com/watch?v=Iv0cPT-7AR8)
[Bitmap presale tutorial](https://medium.com/donkeverse/hardcore-gas-savings-in-nft-minting-part-3-save-30-000-in-presale-gas-c945406e89f0)

## Use storage pointers instead of memory where appropriate

- We have a function that returns the last seen of a user at a given index. 
- It gets the `lastSeen` value and subtracts that from the current `block.timestamp`. 
- Then copies the whole struct into memory, and get the lastSeen which is used in calculating the last seen. 
- This is not so efficient, this is because we are copying all of the struct from storage into memory including variables we don’t need

```java
contract StoragePointerUnOptimized {
    struct User {
        uint256 id;
        string name;
        uint256 lastSeen;
    }

    constructor() {
        users[0] = User(0, "John Doe", block.timestamp);
    }

    mapping(uint256 => User) public users;

    function returnLastSeen(uint256 _id) public view returns (uint256) {
        User memory _user = users[_id];
        uint256 lastSeen = block.timestamp - _user.lastSeen;
        return lastSeen;
    }

    
    function returnLastSeenOptimized(uint256 _id) public view returns (uint256) {
        User storage _user = users[_id]; 
        uint256 lastSeen = block.timestamp - _user.lastSeen;
        return lastSeen;
    }
}

```

Alternatively, use a storage pointer, which results in approximately 5,000 gas savings compared to the first version.
- Here we store the storage pointer for users[_id] in a fixed sized variable on the stack (the pointer of a struct is basically the storage slot of the start of the struct, in this case, this will be the storage slot of user[_id].id). 
- Since storage pointers are lazy (meaning they only act(read or write) when called or referenced).
- Next we only access the lastSeen key of the struct. This way we make a single storage load then store it on the stack, instead of 3 or possibly more storage loads and a memory store before taking a small chunk from memory unto the stack.
> Note: When using storage pointers, it’s important to be careful not to reference dangling pointers.

## Count down instead of counting up, where possible [storage variable]

When setting a storage variable to zero, a refund is given, so the net gas spent on counting will be less if the final state of the storage variable is zero.

## Timestamps and block numbers in storage do not need to be uint256

- A timestamp of size uint48 will work for millions of years into the future.
- A block number increments once every 12 seconds. This should give you a sense of the size of numbers that are sensible.

## Make constructors payable

- Making the constructor payable saved 200 gas on deployment.
- This is because non-payable functions have an implicit require(msg.value == 0) inserted in them.
- Additionally, fewer bytecode at deploy time mean less gas cost due to smaller calldata.

## Admin functions can be payable

We can make admin specific functions payable to save gas, because the compiler won’t be checking the callvalue of the function.
This will also make the contract smaller and cheaper to deploy as there will be fewer opcodes in the creation and runtime code.

### Why? 

- In the case of Non-Payable functions, there are additional opcodes that are executed while calling a non-payable function which ensures that the function shall only be executed if the ether (msg.value) is sent along with the transaction is exactly equal to ZERO.
- However, the same is not true for Payable function. Payable functions allow users to pass in both non-zero or zero ether values while calling the function.
- This basically means that even if zero ether (msg.value == 0) is sent while calling a payable function, the transaction is not reverted. Hence, there is no need to explicitly check the msg.value in the case of Payable functions.

## Down casting variables smaller than uint256 (including boolean and address) are less efficient, unless packed

It is better to use uint256 for integers, except when smaller integers are necessary.
This is because the EVM automatically converts smaller integers to uint256 when they are used. This conversion process adds extra gas cost, so it is more efficient to use uint256 from the start.

So there is no point down-casting to uint8 or whatever, unless you are packing them.

## Bitshifting is cheaper than multiplying or dividing by a power of two

In Solidity, it is often more gas efficient to multiply or divide numbers that are powers of two by shifting their bits, rather than using the multiplication or division operators.

For example, the following two expressions are equivalent

```bash
10 * 2
10 << 1 # shift 10 left by 1
```

and this is also equivalent
```bash
8 / 4
8 >> 2 # shift 8 right by 2
```

Bit shifting operations opcodes in the EVM, such as shr (shift right) and shl (shift left), cost 3 gas while multiplication and division operations (mul and div) cost 5 gas each.
Majority of gas savings also comes the fact that solidity does no overflow/underflow or division by check for shr and shl operations. It’s important to have this in mind when using these operators so that overflow and underflow bugs don't happen.

## Internal functions only used ONCE can be inlined to save gas

It is okay to have internal functions, however they introduce additional jump labels to the bytecode.
Hence, in a case where it is only used by one function, it is better to inline the logic of the internal function inside the function it is being used. This will save some gas by avoiding jumps during the function execution.

## Compare array equality and string equality by hashing them if they are longer than 32 bytes

This is a trick you will rarely use, but looping over the arrays or strings is a lot costlier than hashing them and comparing the hashes.

## Use lookup tables when computing powers and logarithms

If you need to take logarithms or powers where the base or the power is a fraction, it may be preferable to precompute a table if either the base or the power is fixed.
Consider the Bancor Formula and Uniswap V3 Tick Math as examples.